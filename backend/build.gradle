
// 编译脚本
buildscript {
    ext {
        kotlin_version = '1.1.51'
        springBootVersion = "1.5.7.RELEASE"
        springCloudVersion = "Dalston.SR4"
    }

    // 仓库配置
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

description = "SW - 云平台框架"

// 项目配置
allprojects {
    group "com.baomidou.springwind"
    version "1.0"

}

// 子模块配置
subprojects {
    apply plugin: "java"
    apply plugin: 'kotlin'
    apply plugin: "idea"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked"
    }

    // 仓库配置
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    configurations {
        compile.exclude group: "ch.qos.logback"
        compile.exclude group: "com.amazonaws"
        compile.exclude group: "org.apache.tomcat"
        // 使用Undertow 替代Tomcat
        compile.exclude module: "spring-boot-starter-tomcat"
        compile.exclude module: "undertow-websockets-jsr"
    }

    // 依赖管理
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }

        dependencies {
            // 文档
            dependency("io.springfox:springfox-swagger2:2.7.0")
            dependency("io.springfox:springfox-swagger-ui:2.7.0")
            // 工具类
            dependency("commons-collections:commons-collections:3.2.2")
            dependency("org.apache.commons:commons-lang3:3.6")
            // 日志
            dependency("org.slf4j:slf4j-api:1.7.25")
            dependency("com.lmax:disruptor:3.3.7")
        }
    }

    // 依赖配置
    dependencies {
        // 日志
        compile("org.slf4j:slf4j-api")
        compile('org.springframework.boot:spring-boot-starter-log4j2')
        compile("com.lmax:disruptor")
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

        compileOnly("org.springframework.boot:spring-boot-devtools")
        compileOnly("javax.servlet:javax.servlet-api")

        // 编译测试
        testCompile("com.patzn.cloud:patzn-test-parent:1.0")
    }

    // 编译环境 JDK1.8
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
